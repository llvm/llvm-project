// RUN: cir-opt %s -verify-diagnostics -split-input-file

// -----

!u16i = !cir.int<u, 16>
!u32i = !cir.int<u, 32>
!struct1 = !cir.record<struct "Struct1" {!u16i, !u32i}>

// expected-error@+1 {{member type of a #cir.data_member attribute must match the attribute type}}
#invalid_member_ty = #cir.data_member<0> : !cir.data_member<!u32i in !struct1>

// -----

!u16i = !cir.int<u, 16>
!incomplete_struct = !cir.record<struct "Incomplete" incomplete>

// expected-error@+1 {{incomplete 'cir.record' cannot be used to build a non-null data member pointer}}
#incomplete_cls_member = #cir.data_member<0> : !cir.data_member<!u16i in !incomplete_struct>

// -----

!u16i = !cir.int<u, 16>
!u32i = !cir.int<u, 32>
!struct1 = !cir.record<struct "Struct1" {!u16i, !u32i}>

// expected-error@+1 {{member index of a #cir.data_member attribute is out of range}}
#invalid_member_ty = #cir.data_member<2> : !cir.data_member<!u32i in !struct1>

// -----

!u16i = !cir.int<u, 16>
!u32i = !cir.int<u, 32>
!struct1 = !cir.record<struct "Struct1" {!u16i, !u32i}>
!struct2 = !cir.record<struct "Struct2" {!u16i, !u32i}>

module {
  cir.func @invalid_base_type(%arg0 : !cir.data_member<!u32i in !struct1>) {
    %0 = cir.alloca !struct2, !cir.ptr<!struct2>, ["tmp"] {alignment = 4 : i64}
    // expected-error@+1 {{record type does not match the member pointer type}}
    %1 = cir.get_runtime_member %0[%arg0 : !cir.data_member<!u32i in !struct1>] : !cir.ptr<!struct2> -> !cir.ptr<!u32i>
    cir.return
  }
}

// -----

!u16i = !cir.int<u, 16>
!u32i = !cir.int<u, 32>
!struct1 = !cir.record<struct "Struct1" {!u16i, !u32i}>

module {
  cir.func @invalid_base_type(%arg0 : !cir.data_member<!u32i in !struct1>) {
    %0 = cir.alloca !struct1, !cir.ptr<!struct1>, ["tmp"] {alignment = 4 : i64}
    // expected-error@+1 {{result type does not match the member pointer type}}
    %1 = cir.get_runtime_member %0[%arg0 : !cir.data_member<!u32i in !struct1>] : !cir.ptr<!struct1> -> !cir.ptr<!u16i>
    cir.return
  }
}
