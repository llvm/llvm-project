// RUN: cir-opt %s -cir-canonicalize -o - | FileCheck %s

module {
  cir.func @redundant_br() {
    cir.br ^bb1
  ^bb1:  // pred: ^bb0
    %0 = cir.alloca !cir.int<u, 32>, !cir.ptr<!cir.int<u, 32>>, ["a", init] {alignment = 4 : i64}
    %1 = cir.const #cir.int<4> : !cir.int<u, 32>
    cir.store %1, %0 : !cir.int<u, 32>, !cir.ptr<!cir.int<u, 32>>
    cir.br ^bb2
  ^bb2:  // pred: ^bb1
    cir.return
  }
  // CHECK:      cir.func @redundant_br() {
  // CHECK-NEXT:   %[[A:.*]] = cir.alloca !cir.int<u, 32>, !cir.ptr<!cir.int<u, 32>>, ["a", init] {alignment = 4 : i64}
  // CHECK-NEXT:   %[[FOUR:.*]] = cir.const #cir.int<4> : !cir.int<u, 32>
  // CHECK-NEXT:   cir.store %[[FOUR]], %[[A]] : !cir.int<u, 32>, !cir.ptr<!cir.int<u, 32>>
  // CHECK-NEXT:   cir.return
  // CHECK-NEXT: }

  cir.func @empty_scope() {
    cir.scope {
    }
    cir.return
  }
  // CHECK:      cir.func @empty_scope() {
  // CHECK-NEXT:   cir.return
  // CHECK-NEXT: }

  cir.func @cast1(%arg0: !cir.bool) -> !cir.bool {
    %0 = cir.cast(bool_to_int, %arg0 : !cir.bool), !cir.int<s, 32>
    %1 = cir.cast(int_to_bool, %0 : !cir.int<s, 32>), !cir.bool
    cir.return %1 : !cir.bool
  }
  // CHECK:      cir.func @cast1(%[[ARG0:.*]]: !cir.bool) -> !cir.bool
  // CHECK-NEXT:   cir.return %[[ARG0]] : !cir.bool

  cir.func @cast2(%arg0: !cir.int<s, 32>) -> !cir.bool {
    %0 = cir.cast(int_to_bool, %arg0 : !cir.int<s, 32>), !cir.bool
    %1 = cir.cast(bool_to_int, %0 : !cir.bool), !cir.int<s, 32>
    %2 = cir.cast(integral, %1 : !cir.int<s, 32>), !cir.int<s, 64>
    %3 = cir.cast(int_to_bool, %2 : !cir.int<s, 64>), !cir.bool
    cir.return %3 : !cir.bool
  }
  // CHECK:      cir.func @cast2(%[[ARG0:.*]]: !cir.int<s, 32>) -> !cir.bool
  // CHECK-NEXT:   %[[CAST:.*]] = cir.cast(int_to_bool, %[[ARG0]] : !cir.int<s, 32>), !cir.bool
  // CHECK-NEXT:   cir.return %[[CAST]] : !cir.bool

  cir.func @no_fold_cast(%arg0: !cir.int<s, 32>) -> !cir.int<s, 64> {
    %0 = cir.cast(int_to_bool, %arg0 : !cir.int<s, 32>), !cir.bool
    %1 = cir.cast(bool_to_int, %0 : !cir.bool), !cir.int<s, 32>
    %2 = cir.cast(integral, %1 : !cir.int<s, 32>), !cir.int<s, 64>
    cir.return %2 : !cir.int<s, 64>
  }
  // CHECK:      cir.func @no_fold_cast(%[[ARG0:.*]]: !cir.int<s, 32>) -> !cir.int<s, 64>
  // CHECK-NEXT:   %[[CAST:.*]] = cir.cast(int_to_bool, %[[ARG0]] : !cir.int<s, 32>), !cir.bool
  // CHECK-NEXT:   %[[CAST2:.*]] = cir.cast(bool_to_int, %[[CAST]] : !cir.bool), !cir.int<s, 32>
  // CHECK-NEXT:   %[[CAST3:.*]] = cir.cast(integral, %[[CAST2]] : !cir.int<s, 32>), !cir.int<s, 64>
  // CHECK-NEXT:   cir.return %[[CAST3]] : !cir.int<s, 64>

}
