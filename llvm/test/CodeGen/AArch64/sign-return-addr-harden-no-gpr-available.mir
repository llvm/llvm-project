# This test checks the register scavenging performed by the AArch64PointerAuth
# pass when hardening return address authentication, and that it correctly
# skips hardening when no GPRs are available to use.
#
# The test is very contrived in the sense that it forces registers to be alive
# at the return instruction. In order to do so, the test has a control flow
# that doesn't make sense in a real program.

# RUN: llc -mtriple=aarch64 -mattr=+pauth -run-pass=aarch64-ptrauth -verify-machineinstrs -o - %s | FileCheck %s

--- |
  define void @no_gpr_available()
      "sign-return-address"="all"
      "sign-return-address-harden"="load-return-address" {
  entry:
    ret void
  }

  define void @one_gpr_available()
      "sign-return-address"="all"
      "sign-return-address-harden"="load-return-address" {
  entry:
    ret void
  }
...

---
name:            no_gpr_available
tracksRegLiveness: true
body:             |
  ; CHECK-LABEL: name: no_gpr_available
  ; CHECK:       bb.0.entry:
  ; CHECK:         PACIASP
  ; CHECK:         AUTIASP
  ; CHECK-NOT:     ORRXrs
  ; CHECK-NOT:     XPACI
  ; CHECK-NOT:     LDRWui
  ; CHECK-NEXT:    RET $lr
  ; CHECK-LABEL:   bb.1:

  bb.0.entry:
    successors: %bb.1
    liveins: $lr, $sp

    PAUTH_PROLOGUE implicit-def $lr, implicit $lr, implicit $sp
    PAUTH_EPILOGUE implicit-def $lr, implicit $lr, implicit $sp
    RET $lr

  bb.1:
    ; All GPRs are alive at the RET instruction above
    liveins:  $x0, $x1, $x2, $x3, $x4, $x5, $x6, $x7, $x8, $x9, $x10, $x11, $x12, $x13, $x14, $x15, $x16, $x17, $x18, $x19, $x20, $x21, $x22, $x23, $x24, $x25, $x26, $x27, $x28, $lr, $sp
    ERET
...

name:            one_gpr_available
tracksRegLiveness: true
body:             |
  ; CHECK-LABEL: name: one_gpr_available
  ; CHECK:       bb.0.entry:
  ; CHECK:         PACIASP
  ; CHECK:         AUTIASP
  ; CHECK-NEXT:    $x0 = frame-destroy ORRXrs $xzr, $lr, 0
  ; CHECK-NEXT:    $x0 = frame-destroy XPACI $x0
  ; CHECK-NEXT:    $w0 = frame-destroy LDRWui $x0, 0
  ; CHECK-NEXT:    RET $lr
  ; CHECK-LABEL:   bb.1:

  bb.0.entry:
    successors: %bb.1
    liveins: $lr, $sp

    PAUTH_PROLOGUE implicit-def $lr, implicit $lr, implicit $sp
    PAUTH_EPILOGUE implicit-def $lr, implicit $lr, implicit $sp
    RET $lr

  bb.1:
    ; x0 is the only GPR available (not alive) at the RET instruction above.
    liveins:  $x1, $x2, $x3, $x4, $x5, $x6, $x7, $x8, $x9, $x10, $x11, $x12, $x13, $x14, $x15, $x16, $x17, $x18, $x19, $x20, $x21, $x22, $x23, $x24, $x25, $x26, $x27, $x28, $lr, $sp
    ERET
...
