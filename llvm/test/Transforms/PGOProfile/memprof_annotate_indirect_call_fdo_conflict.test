; This test verifies that MemProf value profiles do not override existing FDO
; value profile metadata on indirect calls, but do annotate calls that don't
; have existing FDO metadata.

; RUN: split-file %s %t
; RUN: llvm-profdata merge --memprof-version=4 %t/memprof_fdo_conflict.yaml -o %t/memprof_fdo_conflict.memprofdata
; RUN: opt < %t/memprof_fdo_conflict.ll -passes='memprof-use<profile-filename=%t/memprof_fdo_conflict.memprofdata>' -memprof-attach-calleeguids=true -S 2>&1 | FileCheck %s

;--- memprof_fdo_conflict.yaml
---
HeapProfileRecords:
  - GUID:            _Z3barv
    AllocSites:      []
    CallSites:
      - Frames:
          - { Function: _Z3barv, LineOffset: 3, Column: 5, IsInlineFrame: false }
        CalleeGuids:   [0x123456789abcdef0, 0x23456789abcdef01]
      - Frames:
          - { Function: _Z3barv, LineOffset: 5, Column: 5, IsInlineFrame: false }
        CalleeGuids:   [0x555556789abcdef0, 0x666656789abcdef1]
...
;--- memprof_fdo_conflict.ll
define dso_local void @_Z3barv() !dbg !4 {
entry:
  %fp = alloca ptr, align 8
  %0 = load ptr, ptr %fp, align 8
  ; This call already has FDO value profile metadata - should NOT be modified
  call void %0(), !dbg !5, !prof !6
; CHECK: call void %0(), {{.*}} !prof !6
; CHECK-NOT: call void %0(), {{.*}} !prof ![[NEWPROF:[0-9]+]]

  ; This call does NOT have existing metadata - should get MemProf annotation
  %1 = load ptr, ptr %fp, align 8
  call void %1(), !dbg !7
; CHECK: call void %1(), {{.*}} !prof ![[NEWPROF:[0-9]+]]
  ret void
}

; Existing FDO value profile metadata (should be preserved)
!6 = !{!"VP", i32 0, i64 100, i64 9191153033785521275, i64 80, i64 -1069303473483922844, i64 20}

; CHECK: !6 = !{!"VP", i32 0, i64 100, i64 9191153033785521275, i64 80, i64 -1069303473483922844, i64 20}
; CHECK: ![[NEWPROF]] = !{!"VP", i32 0, i64 2, i64 6148915942236413680, i64 1, i64 7378680115485269745, i64 1}

!llvm.module.flags = !{!2, !3}

!0 = distinct !DICompileUnit(language: DW_LANG_C_plus_plus_14, file: !1)
!1 = !DIFile(filename: "t", directory: "/")
!2 = !{i32 7, !"Dwarf Version", i32 5}
!3 = !{i32 2, !"Debug Info Version", i32 3}
!4 = distinct !DISubprogram(name: "bar", linkageName: "_Z3barv", scope: !1, file: !1, line: 1, unit: !0)
!5 = !DILocation(line: 4, column: 5, scope: !4)
!7 = !DILocation(line: 6, column: 5, scope: !4)
