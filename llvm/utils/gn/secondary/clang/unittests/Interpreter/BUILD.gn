import("//third-party/unittest/unittest.gni")

unittest("ClangReplInterpreterTests") {
  configs += [ "//llvm/utils/gn/build:clang_code" ]
  deps = [
    "//clang/lib/AST",
    "//clang/lib/Basic",
    "//clang/lib/Frontend",
    "//clang/lib/Interpreter",
    "//llvm/lib/IR",
    "//llvm/lib/Target:TargetsToBuild",
    "//llvm/lib/TargetParser",
    "//llvm/lib/Testing/Support",
  ]
  sources = [
    "CodeCompletionTest.cpp",
    "IncrementalCompilerBuilderTest.cpp",
    "IncrementalProcessingTest.cpp",
    "InterpreterExtensionsTest.cpp",
    "InterpreterTest.cpp",
  ]

  # Support plugins.
  # FIXME: Disable dead stripping once other binaries are dead-stripped.
  if (host_os != "mac" && host_os != "win") {
    # Corresponds to export_executable_symbols() in cmake.
    ldflags = [ "-rdynamic" ]
  }
}

# FIXME: This is included conditionally in the CMake build if
#        compiler-rt is available. The test currently fails in the GN
#        build. Figure out if we can get it to pass.
group("dummy_sources") {
  sources = [ "OutOfProcessInterpreterTests.cpp" ]
  not_needed(sources)
}
