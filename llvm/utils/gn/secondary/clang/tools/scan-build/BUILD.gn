group("scan-build") {
  deps = [
    ":bin",
    ":libexec",
    ":man",
    ":share",
  ]
}

copy("bin") {
  sources = [ "bin/scan-build" ]
  if (host_os == "mac") {
    sources += [ "bin/set-xcode-analyzer" ]
  } else if (host_os == "win") {
    sources += [ "bin/scan-build.bat" ]
  }
  outputs = [ "$root_build_dir/bin/{{source_file_part}}" ]
}

copy("libexec") {
  sources = [
    "libexec/ccc-analyzer",
    "libexec/c++-analyzer",
  ]
  if (host_os == "win") {
    sources += [
      "libexec/ccc-analyzer.bat",
      "libexec/c++-analyzer.bat",
    ]
  }
  outputs = [ "$root_build_dir/libexec/{{source_file_part}}" ]
}

copy("man") {
  sources = [ "man/scan-build.1" ]
  outputs = [ "$root_build_dir/share/man/man1/{{source_file_part}}" ]
}

copy("share") {
  sources = [
    "share/scan-build/sorttable.js",
    "share/scan-build/scanview.css",
  ]
  outputs = [ "$root_build_dir/share/scan-build/{{source_file_part}}" ]
}
