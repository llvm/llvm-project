import("//lldb/utils/TableGen/lldb_tablegen.gni")

lldb_tablegen("DynamicLoaderDarwinProperties") {
  args = [ "-gen-lldb-property-defs" ]
}

lldb_tablegen("DynamicLoaderDarwinPropertiesEnum") {
  args = [ "-gen-lldb-property-enum-defs" ]
  td_file = "DynamicLoaderDarwinProperties.td"
}

static_library("MacOSX-DYLD") {
  output_name = "lldbPluginDynamicLoaderMacOSXDYLD"
  configs += [
    "//llvm/utils/gn/build:clang_code",
    "//llvm/utils/gn/build:lldb_code",
  ]
  deps = [
    ":DynamicLoaderDarwinProperties",
    ":DynamicLoaderDarwinPropertiesEnum",
    "//lldb/source/Breakpoint",
    "//lldb/source/Core",
    "//lldb/source/Expression",
    "//lldb/source/Host",
    "//lldb/source/Plugins/TypeSystem/Clang",
    "//lldb/source/Symbol",
    "//lldb/source/Target",
    "//lldb/source/Utility",
    "//llvm/lib/Support",
    "//llvm/lib/TargetParser",
  ]

  # Reaches into Plugins/LanguageRuntime/ObjC.
  include_dirs = [ "//lldb/source" ]
  sources = [
    "DynamicLoaderDarwin.cpp",
    "DynamicLoaderDarwinProperties.cpp",
    "DynamicLoaderMacOS.cpp",
    "DynamicLoaderMacOSXDYLD.cpp",
  ]
}
