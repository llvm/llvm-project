executable("llvm-jitlink") {
  deps = [
    "//llvm/lib/BinaryFormat",
    "//llvm/lib/ExecutionEngine",
    "//llvm/lib/ExecutionEngine/JITLink",
    "//llvm/lib/ExecutionEngine/Orc",
    "//llvm/lib/ExecutionEngine/RuntimeDyld",
    "//llvm/lib/MC",
    "//llvm/lib/Object",
    "//llvm/lib/Support",
    "//llvm/lib/Target:TargetsToBuild",
  ]
  sources = [
    "llvm-jitlink-elf.cpp",
    "llvm-jitlink-macho.cpp",
    "llvm-jitlink.cpp",
  ]
  if (host_os == "win") {
    ldflags = [ "/export:llvm_orc_registerJITLoaderGDBWrapper" ]
  } else if (host_os != "mac") {
    # Corresponds to export_executable_symbols() in cmake.
    ldflags = [ "-rdynamic" ]
  }
}
