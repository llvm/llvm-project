import("//llvm/utils/TableGen/tablegen.gni")
import("//llvm/utils/gn/build/driver_executable.gni")
import("//llvm/utils/gn/build/symlink_or_copy.gni")

tablegen("Opts") {
  visibility = [ ":llvm-ml" ]
  args = [ "-gen-opt-parser-defs" ]
}

symlink_or_copy("llvm-ml64") {
  deps = [ ":llvm-ml" ]
  source = "llvm-ml"
  output = "$root_out_dir/bin/llvm-ml64"
}

# //:llvm-ml depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [ ":llvm-ml64" ]
}

driver_executable("llvm-ml") {
  deps = [
    ":Opts",
    "//llvm/lib/MC",
    "//llvm/lib/MC/MCParser",
    "//llvm/lib/Option",
    "//llvm/lib/Support",
    "//llvm/lib/Target:TargetsToBuild",
    "//llvm/lib/TargetParser",
  ]
  sources = [
    "Disassembler.cpp",
    "llvm-ml.cpp",
  ]
}
